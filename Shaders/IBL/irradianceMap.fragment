// Generates an irradiance cube from an environment map using convolution

#version 450

layout (location = 0) in vec3 inPos;
layout (location = 0) out vec4 outColor;
layout (binding = 0) uniform sampler2D samplerEnv; // 改为2D纹理采样器

layout(push_constant) uniform PushConsts
{
    layout (offset = 64) float deltaPhi;
    layout (offset = 68) float deltaTheta;
} consts;

#define PI 3.1415926535897932384626433832795

void main()
{
    vec3 N = normalize(inPos);
    vec3 up = vec3(0.0, 1.0, 0.0);
    vec3 right = normalize(cross(up, N));
    up = cross(N, right);

    const float TWO_PI = PI * 2.0;
    const float HALF_PI = PI * 0.5;

    vec3 color = vec3(0.0);
    uint sampleCount = 0u;
    
    for (float phi = 0.0; phi < TWO_PI; phi += consts.deltaPhi) 
    {
        for (float theta = 0.0; theta < HALF_PI; theta += consts.deltaTheta) 
        {
            vec3 tempVec = cos(phi) * right + sin(phi) * up;
            vec3 sampleVector = cos(theta) * N + sin(theta) * tempVec;
            
            // 将方向向量转换为equirectangular UV坐标
            vec3 dir = normalize(sampleVector);
            float phi_uv = atan(dir.z, dir.x); // 方位角
            float theta_uv = acos(dir.y);      // 天顶角
            
            float u = (phi_uv + PI) / (2.0 * PI);
            float v = theta_uv / PI;
            vec2 uv = vec2(u, v);
            
            color += texture(samplerEnv, uv).rgb * cos(theta) * sin(theta);
            sampleCount++;
        }
    }
    outColor = vec4(PI * color / float(sampleCount), 1.0);
}